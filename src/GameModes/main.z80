SECTION "GM_MAIN",ROM0

Game_LoadBG:
    ld hl, bgmap_start
    ld de, LvlOrient
    ; lvlorient
    ld a, [hl+]
    ld [de], a
    inc de
    ; lvllen
    ld a, [hl+]
    ld [de], a
    inc de
    ; lvl buffer
    ld bc, 20 * 16
    call memcpy

    ret

; Copy one row from memory to the display (for vertical levels)
; TODO: probably move this and related things into a new file
; b: src row
; c: dst row
Game_BlitBG_Row:
    ; set hl to src address
    ld h, b
    ld a, [LvlLength]
    call Mult8
    ld de, LvlGfxMap
    add hl, de

    ; set de to src address
    push hl
    ld hl, _SCRN0
    sla e ; x2
    sla e ; x4
    sla e ; x8
    sla e ; x16
    sla e ; x32
    ld d, 0
    add hl, de
    ld d, h
    ld e, l
    pop hl

    ld bc, LVL_MIN_WIDTH
    call memcpy
    ret

Game_BlitBG_Horiz:


Game_LoadHUD:
    ; make a super basic window tilemap
    ld hl, _SCRN1
    ld bc, $0400
    ld a, $80
    call memset ; AAAAAAAAA

    ld a, 0
    ldh [rWY], a
    ld a, 7
    ldh [rWX], a

Game_LoadGFX:
    di
    WaitForVBlank
    ei
    call DisableLCD

    ; Set scroll position
    xor a
    ld [rSCX], a
    ld [rSCY], a

    ; Load graphics
    ld hl, gfx_start
    ld de, _VRAM
    ld bc, gfx_end - gfx_start
    call memcpy

    ld hl, font
    ld de, _VRAM + $800
    ld bc, font_end - font
    call memcpy

    ld hl, bg_start
    ld de, _VRAM + $1000
    ld bc, bg_end - bg_start
    call memcpy

    ; Load background
    call Game_LoadBG

    ; set interrupt for line whatever to turn window off
    ld a, 16
    ldh [WindowY], a
    ldh [WindowYTarget], a
    ldh [rLYC], a
    ld a, STATF_LYC
    ldh [rSTAT], a 
ret

; super basic
CheckPaused:
    ; Start pressed this frame but not last frame?
    ldh a, [JoypadButtons]
    ld b, a
    ldh a, [LastButtons]
    cpl
    and a, b ; JoypadButtons & ~LastButtons
    and a, PADF_START
    jp z, .return

    ; actually check pause flag
    ldh a, [IsPaused]
    and a
    jp z, .do_pause

.do_unpause:
    xor a
    ldh [IsPaused], a ; 0

    ; fix moonwalk glitch by cancelling left/right
    ldh a, [JoypadButtons]
    and a, ~(PADF_LEFT | PADF_RIGHT)
    ldh [JoypadButtons], a 

    ; set window position
    ld a, 16 ; lines for gameplay
    ldh [WindowYTarget], a

    ; unmute music
    ld b, 0
    ld c, 0
    call hUGE_mute_channel
    ld b, 1
    ld c, 0
    call hUGE_mute_channel
    ld b, 2
    ld c, 0
    call hUGE_mute_channel
    ld b, 3
    ld c, 0
    call hUGE_mute_channel

    jp .return

.do_pause:
    ld a, 1
    ldh [IsPaused], a

    ; set window position
    ld a, 40 ; lines for pause menu
    ldh [WindowYTarget], a

    ; unmute music
    ld b, 0
    ld c, 1
    call hUGE_mute_channel
    ld b, 1
    ld c, 1
    call hUGE_mute_channel
    ld b, 2
    ld c, 1
    call hUGE_mute_channel
    ld b, 3
    ld c, 1
    call hUGE_mute_channel

.return
    ret

SetWindowPos:
    ldh a, [WindowYTarget]
    ld b, a
    ldh a, [WindowY]
    sub a, b
    jp z, .return
    jp c, .increase

.decrease:
    ldh a, [WindowY]
    dec a
    ldh [WindowY], a
    ret

.increase:
    ldh a, [WindowY]
    inc a
    ldh [WindowY], a

.return:
    ret


GameLoop:
    ; Load in-game GFX
    call Game_LoadGFX

    ; Load music
    ;ld hl, ryukenden
    ;call hUGE_init
    
    ; Init objects
    ld hl, SampleObject
    ld de, charArray
    ld bc, _get(`charStruct', `sizeof')
    call memcpy

    xor a, a
    ldh [IsPaused], a

    call EnableLCD
    ei

.loop:
    ; Reset scroll layer
	xor a
	ldh [rSCX], a

    ; Re-enable window
    ldh a, [rLCDC]
    set LCDCB_WINON, a
    ldh [rLCDC], a

    ; Set new window position
    call SetWindowPos

    ; set window position
    ldh a, [WindowY]
    ldh [rLYC], a

    ; redraw background
    call Game_BlitBG_Horiz

.loop_noncrit:
    ; Tick global timer
    ldh a, [GlobalTimer]
    inc a
    ldh [GlobalTimer], a

    ; Get frame data ready
    call GenerateActiveObjectsBitfield 
    call readJoypad

    ; check if we need to pause
    call CheckPaused
    ldh a, [IsPaused]
    and a
    jp nz, .VBlankWaitPrepare

    ; Game logic
    call TickAllObjects

    ; Prepare graphics data
    call PrepareAllObjects

    ; Music tick
    ;call _hUGE_dosound

    ; Wait for VBlank before looping
    ; TODO: Create an exit condition
    ; Wait for VBlank
.VBlankWaitPrepare
    xor a
    ld [WaitingForVBlank], a
.VBlankWait
    halt
    ld a, [WaitingForVBlank]
    or a
    jr z, .VBlankWait

    jr .loop
